public class SphereParticle extends Particle
{
  public void draw()
  {
    fill(fill);
    pushMatrix();
    translate(pos.value[0],pos.value[1],pos.value[2]);
    noStroke();
    sphere(radius);
    popMatrix();
    
  }
}

public class DotParticle extends Particle
{
  public void draw()
  {
    stroke(fill);
    strokeWeight(5);
    pushMatrix();
    translate(pos.value[0],pos.value[1],pos.value[2]);
    point(0,0,0);
    popMatrix();
    
  }
}

public class TexturedParticle extends Particle
{
  public TexturedParticle()
  {
    this.bounce   = .01;
    this.friction = .99;
    this.radius = 1;
    this.life = 100;
  }
  
  public void draw()
  {
    noStroke();
    fill(fill);
    pushMatrix();
    translate(pos.value[0],pos.value[1],pos.value[2]); // place particle in the correct location
    //rotateZ(-camera.yaw);
    rotateY(PI/2.0); //  rotate so that the texture is billboarded to the camera
    
    beginShape();
    
    texture(texture);
    tint(255, 100);    
    vertex(0,-radius, -radius, 0,             0);
    vertex(0, radius, -radius, texture.width, 0);
    vertex(0, radius,  radius, texture.width, texture.height);
    vertex(0,-radius,  radius, 0,             texture.height);
    
    endShape();
    popMatrix();
    
  }


}



public class FireWork extends SphereParticle
{
  
  public FireWork(){}
  
  public void update(float time_step)
  { // FORWARD EULER
    life--;
    Vector newPos = pos.plus(vel.multiply(time_step));
    if(newPos.value[1] < radius) // if hits ground (z = 0)
    {
      vel.value[1] *= - bounce;     // change velocity to positive and scaled based on particle bounciness
      newPos.value[1] = radius;     // nudge the particle out of the way
      vel = vel.multiply(friction); // slow the velocity due to imperfect collision
    }
    else
      vel = vel.plus(acc.multiply(time_step));
    
    if(this.life > 0)
    { // add a trail
      Particle p = new TexturedParticle();
      p.radius = 1;
      p.pos = pos;
     
      p.acc = new Vector();
      p.life = this.life + (int)random(50,100);
      particles.add(p);
      pos = newPos;
    }
    else
    {
      for(int i =0; i < 20 ; i ++)
      {
        Particle p = new TrailedParticle();
        p.radius = 1;
        p.fill = color(0,255,0);
        p.pos = pos;
        p.vel = this.vel.plus(new Vector(random(-1,1),random(-1,1),random(-1,1)));
        p.acc = gravity.multiply(.05);
        p.life = this.life + (int)random(50,100);
        particles.add(p);
        pos = newPos;
      }
    }
  }

}

public class TrailedParticle extends DotParticle
{
  
  public void update(float time_step)
  { // FORWARD EULER
    life--;
    this.fill = color(0,life*4+10,0);
    Vector newPos = pos.plus(vel.multiply(time_step));
    if(newPos.value[1] < radius) // if hits ground (z = 0)
    {
      vel.value[1] *= - bounce;     // change velocity to positive and scaled based on particle bounciness
      newPos.value[1] = radius;     // nudge the particle out of the way
      vel = vel.multiply(friction); // slow the velocity due to imperfect collision
    }
    else
      vel = vel.plus(acc.multiply(time_step));
    
    // add a trail
    if (random(0,2) > 1)
    {
      Particle p = new TexturedParticle();
      p.fill = color(0,255,0);
      p.pos = pos;
      p.acc = new Vector();
      p.life = this.life + (int)random(0,50);
      p.radius = this.radius;
      particles.add(p);
    }
    pos = newPos;
  }
  
  
}